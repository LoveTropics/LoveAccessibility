buildscript {
  repositories {
    maven { url = 'https://maven.minecraftforge.net' }
    maven { url = 'https://maven.parchmentmc.org' }
    maven { url = 'https://repo.spongepowered.org/maven' }
    mavenCentral()
  }
  dependencies {
    classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    classpath 'org.parchmentmc:librarian:1.+'
    classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
  }
}

plugins {
  id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'

version = "0.1.0"
group = "com.lovetropics"
archivesBaseName = "love-accessibility"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
  mappings channel: 'parchment', version: "${parchment_version}-${minecraft_version}"
  runs {
    client {
      workingDirectory project.file('run')
      property 'forge.logging.console.level', 'info'
      arg "-mixin.config=love_accessibility.mixins.json"
      jvmArg '-Dmixin.env.disableRefMap=true'
      mods {
        love_accessibility {
          sources sourceSets.main
        }
      }
    }
    server {
      workingDirectory project.file('run')
      property 'forge.logging.console.level', 'info'
      arg "-mixin.config=love_accessibility.mixins.json"
      jvmArg '-Dmixin.env.disableRefMap=true'
      mods {
        love_accessibility {
          sources sourceSets.main
        }
      }
    }
  }
}

configurations {
  shade
  library
  implementation.extendsFrom library
}

repositories {
  maven { url = 'https://maven.gegy.dev' }
}

dependencies {
  minecraft "net.minecraftforge:forge:$minecraft_version-$forge_version"

  final def nvda_controller = 'dev.gegy:nvda-controller-client-java:1.0.0'
  library nvda_controller
  shade(nvda_controller) {
    transitive = false
  }

  if (System.getProperty("idea.sync.active") != "true") {
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
  }
}

minecraft.runs.all {
  lazyToken('minecraft_classpath') {
    configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
  }
}

mixin {
  add sourceSets.main, "love_accessibility.refmap.json"
}

shadowJar {
  configurations = [project.configurations.shade]
  classifier = ''
}

reobf {
  shadowJar {}
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar

jar {
  classifier = 'slim'
  manifest {
    attributes([
        "Specification-Title"     : "love-accessibility",
        "Specification-Vendor"    : "lovetropics",
        "Specification-Version"   : "1",
        "Implementation-Title"    : project.name,
        "Implementation-Version"  : "${version}",
        "Implementation-Vendor"   : "lovetropics",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        "MixinConfigs"            : "love_accessibility.mixins.json"
    ])
  }
}
